// Copyright 2012 Mark Armbrust. Permission granted for educational use.

/** Random.jack -- A not so random PRNG(pseudo random number generation). */

class Random {

    static int seed;

    function void setSeed(int newSeed) {
        let seed = newSeed;
        return;
    }

    function int rand() {
        /** return a random number in the range 0..32767 , */
        let seed = seed + 20251; /** when add 20k,if out of 16 bit limit goes to negative so adding*/
        if (seed < 0) {
            let seed = seed - 32767 - 1;/** 32767 so it stays in positive range*/
        }
        return seed;
    }

    function int randRange(int range) {
        /** return a random number in the range 0..range */
        var int mask;
        var int ret;
        let mask = 1;
        while (mask < range) {
            let mask = mask * 2 + 1;
        }
        let ret = Random.rand() & mask;
        while (ret > range) {
            let ret = Random.rand() & mask;
        }
        return ret; /** to determine initial position or random delay btw game cycles  */
    }

}